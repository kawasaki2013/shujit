dnl Process this file with autoconf to produce a configure script for shuJIT

AC_INIT(compiler.c)

AC_CONFIG_HEADER(config.h)

dnl AC_CANONICAL_HOST
AC_CANONICAL_SYSTEM


AC_ARG_ENABLE(xbox,
	[  --enable-xbox           compile for Xbox with Cygwin (static link)],
	[
	ac_xbox=yes
	AC_DEFINE(XBOX)
	],
	[])
AC_SUBST(ac_xbox)


AC_ARG_ENABLE(codedb,
	[  --disable-codedb        disable code DB ],
	[],
	[
	case "$host_os" in
		cygwin)
			ac_codedb=no
			;;
		*)
			LIBS="$LIBS -L/usr/local/lib"
			AC_CHECK_LIB(gdbm, gdbm_open,
				[
				ac_libgdbm=yes
				AC_DEFINE(GDBM)
				],
				[
				ac_libgdbm=
				AC_CHECK_LIB(ndbm, dbm_open,
					[
					ac_libndbm=yes
					AC_DEFINE(NDBM)
					],
					[
					ac_libndbm=
					dnl AC_MSG_ERROR(could find neither gdbm nor ndbm)
					dnl default is NDBM
					ac_libndbm=yes
					dnl AC_DEFINE(GDBM)
					])
				])
			ac_codedb=yes
			AC_DEFINE(CODE_DB)
			;;
	esac
	])
AC_SUBST(ac_codedb)
AC_SUBST(ac_libgdbm)


AC_ARG_ENABLE(sse2,
	[  --enable-sse2           use SSE2 instructions instead of x87 ],
	[
	ac_sse2=yes
	AC_DEFINE(USE_SSE2)
	],
	[])
AC_SUBST(ac_sse2)


AC_ARG_ENABLE(moba,
	[  --enable-moba           support thread migration ],
	[
	ac_moba=yes
	AC_DEFINE(MOBA)
	],
	[])
AC_SUBST(ac_moba)


AC_ARG_ENABLE(metavm_no_array,
	[  --enable-metavm-no-array
                          enable MetaVM and arrays are passed by value ],
	[
	ac_metavm=yes
	ac_metavm_no_array=yes
	AC_DEFINE(METAVM)
	AC_DEFINE(METAVM_NO_ARRAY)
	],
	[ ac_metavm_no_array= ])
AC_ARG_ENABLE(metavm,
	[  --enable-metavm         enable MetaVM ],
	[
	ac_metavm=yes
	AC_DEFINE(METAVM)
	])
AC_SUBST(ac_metavm)
AC_SUBST(ac_metavm_no_array)


dnl Checks for programs.
AC_PROG_MAKE_SET
AC_MSG_CHECKING(C compiler)
ac_cc=no
if test -n "$CC"; then
	if test -f "$CC"; then
		ac_cc=$CC
	else
		AC_PATH_PROG(ac_cc, $CC, no)
	fi
fi
if test "$ac_cc" = "no"; then
	AC_PATH_PROGS(ac_cc, gcc3 gcc egcs egcc, no)
fi
if test "$host_os" = "cygwin"; then
	ac_cc="$ac_cc -mno-cygwin"
fi
AC_MSG_RESULT($ac_cc)
AC_SUBST(ac_cc)

AC_PATH_PROGS(ac_objdump, objdump, no)
AC_SUBST(ac_objdump)
if test "$ac_ruby" = no; then
	AC_MSG_ERROR(Objdump (provided by GNU binutils) is required.)
fi
AC_PATH_PROG(ac_ruby, ruby, no)
if test "$ac_ruby" = no; then
	AC_MSG_ERROR(Ruby (see http://www.ruby-lang.org/) is required.)
fi
AC_SUBST(ac_ruby)
AC_PATH_PROG(ac_strip, strip, no)
AC_SUBST(ac_strip)
AC_PATH_PROG(ac_ci, ci, no)
AC_SUBST(ac_ci)
AC_PATH_PROG(ac_co, co, no)
AC_SUBST(ac_co)
AC_PATH_PROG(ac_mv, mv, no)
AC_SUBST(ac_mv)
AC_PATH_PROG(ac_rm, rm, no)
AC_SUBST(ac_rm)
AC_PATH_PROG(ac_wc, wc, no)
AC_SUBST(ac_wc)
AC_PATH_PROG(ac_etags, etags, no)
AC_SUBST(ac_etags)

AC_PATH_PROG(ac_which, which, no)
AC_PATH_PROGS(ac_grep, fgrep grep egrep, no)
AC_PATH_PROG(ac_sed, sed, no)
AC_PROG_AWK


AC_MSG_CHECKING(version of gcc)
ac_gccver_string=`$ac_cc -v 2>&1 | $ac_grep 'gcc version'`
case `echo $ac_gccver_string | $AWK '{print $3}' | $ac_sed 's/.*-//'` in
changequote(, )dnl
	[3-9].*)
changequote([, ])dnl
		AC_MSG_RESULT(gcc >= 3.0)
		ac_gccver=300
		;;
	2.96*)
		AC_MSG_RESULT(gcc 2.96)
		ac_gccver=296
		;;
	2.95*)
		AC_MSG_RESULT(gcc 2.95)
		ac_gccver=295
		;;
	2.91*)
		AC_MSG_RESULT(egcs)
		ac_gccver=291
		;;
	2.7*|2.6*|2.5*|2.4*)
		dnl AC_MSG_ERROR(GCC 2.95 or newer is required.)
		AC_MSG_RESULT(gcc <= 2.7)
		ac_gccver=270
		;;
	*)
		AC_ERROR(unrecognized.)
		;;
esac
AC_SUBST(ac_gccver)
AC_SUBST(ac_gccver_string)
AC_DEFINE_UNQUOTED(GCC_VER, $ac_gccver)
AC_DEFINE_UNQUOTED(GCC_VER_STRING, "$ac_gccver_string")


AC_MSG_CHECKING(OS type)
case $host_os in
	cygwin)
		if test "$ac_xbox" = yes; then
			ac_cflags="-DSTATIC_LINK -Dx86 -D__MINGW_IMPORT=extern"
		else
			ac_cflags=
		fi
		ac_asflags=
		ac_target_shujit=shujit.dll
		ac_target_shujit_g=shujit_g.dll
		ac_target_metavm=metavm.dll
		ac_target_metavm_g=metavm_g.dll
		;;
	freebsd2*)
		ac_cflags=-fPIC
		ac_asflags=-k
		ac_target_shujit=libshujit.so
		ac_target_shujit_g=libshujit_g.so
		ac_target_metavm=libmetavm.so
		ac_target_metavm_g=libmetavm_g.so
		;;
	*)
		ac_cflags=-fPIC
		ac_asflags=
		ac_target_shujit=libshujit.so
		ac_target_shujit_g=libshujit_g.so
		ac_target_metavm=libmetavm.so
		ac_target_metavm_g=libmetavm_g.so
		;;
esac
AC_SUBST(ac_asflags)
AC_SUBST(ac_cflags)
AC_SUBST(ac_target_shujit)
AC_SUBST(ac_target_shujit_g)
AC_SUBST(ac_target_metavm)
AC_SUBST(ac_target_metavm_g)


AC_MSG_CHECKING(install path of JDK)
AC_ARG_WITH(jdk,
	[  --with-jdk              path where JDK installed to ],
	[ ac_jhome=${with_jdk} ],
	[
	case "$host_os" in
		cygwin)
			ac_jhome=`ls -d /cygdrive/c/jdk*`
			;;
		*)
			ac_jhome=`$ac_which java | $ac_sed -e 's+/bin/.*++' | $ac_sed -e 's+/jre++'`
			;;
	esac
	])
AC_MSG_RESULT($ac_jhome)

ac_jbin="$ac_jhome/bin"

case "$host_os" in
	cygwin)
		ac_java=java
		ac_javac=$ac_jbin/javac
		ac_javah=$ac_jbin/javah
		ac_jar=$ac_jbin/jar
		ac_javadoc=$ac_jbin/javadoc
		;;
	*)
		AC_PATH_PROGS(ac_java, java jre, no, $ac_jbin)
		if test "$ac_java" = no; then
			AC_MSG_ERROR(java command is not found.)
		fi
		AC_PATH_PROG(ac_javac, javac, no, $ac_jbin)
		AC_PATH_PROG(ac_javah, javah, no, $ac_jbin:$PATH)
		AC_PATH_PROG(ac_jar, jar, no, $ac_jbin:$PATH)
		AC_PATH_PROG(ac_javadoc, javadoc, no, $ac_jbin:$PATH)
		;;
esac
AC_SUBST(ac_java)
AC_SUBST(ac_javac)
AC_SUBST(ac_javah)
AC_SUBST(ac_jar)
AC_SUBST(ac_javadoc)

AC_MSG_CHECKING(version of JDK)
ac_jver_string=`$ac_java -Djava.compiler= -version 2>&1 | $ac_grep 'version'`
case "$ac_jver_string" in
	*\"1.1*)
		ac_jver=11
		AC_MSG_RESULT(JDK 1.1)
		;;
	*\"1.2*)
		ac_jver=12
		AC_MSG_RESULT(JDK 1.2)
		;;
	*\"1.3*)
		ac_jver=13
		AC_MSG_RESULT(JDK 1.3)

		case $host_os in
			freebsd*)
				case $ac_jver_string in
					*\"1.3.1-p*)
dnl					only 1.3.1-p6, or 1.3.1-p6 and later?
						AC_MSG_RESULT(a workaround for a problem of FreeBSD JDK 1.3.1-p6 enabled)
						AC_DEFINE(WORKAROUND_FOR_FREEBSD_131P6)
					;;
				esac
			;;
		esac

		;;
	*)
		AC_MSG_ERROR(unrecognized.)
		;;
esac
AC_SUBST(ac_jver)
AC_DEFINE_UNQUOTED(JDK_VER, $ac_jver)

ac_jincdir2="$ac_jhome/include"
if test "$ac_jver" = "11"; then
	ac_jincdir1="$ac_jhome/include"
else
	ac_jincdir1="$ac_jhome/include-old"
fi
AC_SUBST(ac_jincdir1)
AC_SUBST(ac_jincdir2)

case "$host_os" in
	netbsd*)
		ac_jincdir_md=netbsd
		;;
	freebsd*)
		ac_jincdir_md=freebsd
		;;
	linux*)
		if test -d "$ac_jincdir1/linux"; then
			ac_jincdir_md=linux
		else
			ac_jincdir_md=genunix
		fi
		;;
	cygwin)
		ac_jincdir_md=win32
		;;
esac
AC_SUBST(ac_jincdir_md)

AC_MSG_CHECKING([asm or C, which version of interpreter is used])
AC_ARG_ENABLE(c-interpreter,
	[  --enable-c-interpreter  the JDK uses C version of interpreter ],
	[
	ac_executejava_in_asm=no
	],
	[
	ac_executejava_in_asm=yes
	case "$host_os" in
		netbsd*)
			ac_executejava_in_asm=no
			;;
		freebsd*)
			ac_jver_string=`$ac_java -Djava.compiler= -fullversion 2>&1`
changequote(, )dnl
			ac_jver_string=`echo $ac_jver_string | sed 's+.*:\([0-9]*\)/\([0-9]*\)/.*+\1/\2+'`
changequote([, ])dnl
			echo -n "(date: $ac_jver_string) "
			case "$ac_jver_string" in
				1998/*|1999/3|1999/4|1999/6|1999/7)
					ac_executejava_in_asm=no
			esac
			;;
	esac
	])
if test "$ac_executejava_in_asm" = "yes"; then
	AC_MSG_RESULT(assembly code version)
	AC_DEFINE(EXECUTEJAVA_IN_ASM)
else
	AC_MSG_RESULT(C version)
fi


dnl Checks for libraries.

AC_MSG_CHECKING(version of libc)
AC_EGREP_CPP(version of glibc is 2,
	[
	#include <stdio.h>
	version of glibc is __GLIBC__
	],
	ac_glibc2=yes)
if test "$ac_glibc2" = "yes"; then
	AC_MSG_RESULT(glibc2)
	AC_DEFINE(GLIBC2)
else
	AC_MSG_RESULT(not glibc2)
fi

AC_MSG_CHECKING(POSIX.1b Scheduling Interface)
ac_posix_sched=
case "$host_os" in
	cygwin)
		;;
	*)
		AC_CHECK_HEADER(sched.h,
			[
			AC_CHECK_FUNC(sched_setscheduler,
				[
				ac_posix_sched=yes
				AC_DEFINE(HAVE_POSIX_SCHED)
				])
			])
		;;
esac


dnl Checks for header files.

AC_MSG_CHECKING(whether the <stdint.h> header exists)
AC_CHECK_HEADER(stdint.h,
	[
	AC_DEFINE(HAVE_STDINT_H)
	])

AC_MSG_CHECKING(whether the <sys/types.h> header exists)
AC_CHECK_HEADER(sys/types.h,
	[
	AC_DEFINE(HAVE_SYS_TYPES_H)
	])

AC_MSG_CHECKING(type of an argument of monitorEnter())
ac_monitor_t=`$ac_grep monitorEnter $ac_jincdir1/monitor.h | $ac_sed 's/(/ /' | $ac_sed "s/.* \(.*\));.*/\1/"`
AC_MSG_RESULT($ac_monitor_t)
AC_DEFINE_UNQUOTED(MONITOR_T, $ac_monitor_t)

AC_MSG_CHECKING(whether the <asm/ucontext.h> header exists)
if test -f "/usr/include/asm/ucontext.h"; then
	AC_MSG_RESULT(exists)
	AC_DEFINE(HAVE_UCONTEXT_H)
else
	AC_MSG_RESULT(does not exist)
fi

AC_MSG_CHECKING(whether the JDK_HOME/include/green_threads directory exists)
if test -d "$ac_jincdir1/green_threads"; then
	AC_MSG_RESULT(exists)
	AC_DEFINE(HAVE_GREENTHR_HEADER)
else
	AC_MSG_RESULT(does not exist)
fi


dnl Checks for typedefs, structures, and compiler characteristics.


AC_OUTPUT(GNUmakefile metavm/GNUmakefile NET/shudo/metavm/GNUmakefile)
